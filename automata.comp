#version 450

#include "FastNoiseLite.glsl"
#include "config.hpp"

layout(local_size_x = THREADS, local_size_y = THREADS, local_size_z = THREADS) in;
layout(set = 0, binding = 0, r8ui) uniform readonly uimage3D inCells;
layout(set = 1, binding = 0, r8ui) uniform writeonly uimage3D outCells;
layout(set = 2, binding = 0) uniform uniformRules
{
    uint seed;
    uint surviveMask;
    uint birthMask;
    uint life;
    uint neighborhood;
    uint frame;
};

const ivec3 Moore[26] = ivec3[](
    ivec3(-1,-1,-1), ivec3( 0,-1,-1), ivec3( 1,-1,-1),
    ivec3(-1, 0,-1), ivec3( 0, 0,-1), ivec3( 1, 0,-1),
    ivec3(-1, 1,-1), ivec3( 0, 1,-1), ivec3( 1, 1,-1),
    ivec3(-1,-1, 0), ivec3( 0,-1, 0), ivec3( 1,-1, 0),
    ivec3(-1, 0, 0),                  ivec3( 1, 0, 0),
    ivec3(-1, 1, 0), ivec3( 0, 1, 0), ivec3( 1, 1, 0),
    ivec3(-1,-1, 1), ivec3( 0,-1, 1), ivec3( 1,-1, 1),
    ivec3(-1, 0, 1), ivec3( 0, 0, 1), ivec3( 1, 0, 1),
    ivec3(-1, 1, 1), ivec3( 0, 1, 1), ivec3( 1, 1, 1)
);

const ivec3 VonNeumann[6] = ivec3[](
    ivec3(-1, 0, 0),
    ivec3( 1, 0, 0),
    ivec3( 0,-1, 0),
    ivec3( 0, 1, 0),
    ivec3( 0, 0,-1),
    ivec3( 0, 0, 1)
);

void main()
{
    ivec3 id = ivec3(gl_GlobalInvocationID);
    if (any(greaterThanEqual(id, ivec3(BOUNDS))))
    {
        return;
    }
    if (frame == 0)
    {
        float frequency = 0.1f;
        float x = float(id.x) * frequency;
        float y = float(id.y) * frequency;
        float z = float(id.z) * frequency;
        float value = _fnlSinglePerlin3D(int(seed), x, y, z);
        imageStore(outCells, id, uvec4(value > 0.65f));
        return;
    }
    if (frame == 1)
    {
        int value = int(imageLoad(inCells, id).x);
        imageStore(outCells, id, uvec4(value));
        return;
    }
    uint neighbors = 0;
    switch (neighborhood)
    {
    case MOORE:
        for (int i = 0; i < 26; i++)
        {
            ivec3 neighborId = id + Moore[i];
            if (any(lessThan(neighborId, ivec3(0))) || any(greaterThanEqual(neighborId, ivec3(BOUNDS))))
            {
                continue;
            }
            neighbors += uint(imageLoad(inCells, neighborId).x > 0);
        }
        break;
    case VON_NEUMANN:
        for (int i = 0; i < 6; i++)
        {
            ivec3 neighborId = id + VonNeumann[i];
            if (any(lessThan(neighborId, ivec3(0))) || any(greaterThanEqual(neighborId, ivec3(BOUNDS))))
            {
                continue;
            }
            neighbors += uint(imageLoad(inCells, neighborId).x > 0);
        }
        break;
    }
    int value = int(imageLoad(inCells, id).x);
    if (value == 0 && ((birthMask & (1u << neighbors)) != 0))
    {
        value = int(life);
    }
    else if ((surviveMask & (1u << neighbors)) == 0)
    {
        value--;
    }
    value = max(0, value);
    imageStore(outCells, id, uvec4(value));
}